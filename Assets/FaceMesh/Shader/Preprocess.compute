#pragma kernel Preprocess

//
// Preprocessor (texture to tensor conversion)
//

// Face Mesh input image size (defined by the model)
#define IMAGE_SIZE 192

// Input
sampler2D _Texture;

// Output
RWBuffer<float> _Tensor;

[numthreads(8, 8, 1)]
void Preprocess(uint3 id : SV_DispatchThreadID)
{
    // UV (vertically flipped)
    float2 uv = float2(0.5 + id.x, IMAGE_SIZE - 0.5 - id.y) / IMAGE_SIZE;

    // UV gradients
    float2 duv_dx = float2(1.0 / IMAGE_SIZE, 0);
    float2 duv_dy = float2(0, -1.0 / IMAGE_SIZE);

    // Texture sample
    float3 rgb = tex2Dgrad(_Texture, uv, duv_dx, duv_dy).rgb * 2 - 1;

    // Tensor element output
    uint offs = (id.y * IMAGE_SIZE + id.x) * 3;
    _Tensor[offs + 0] = rgb.r;
    _Tensor[offs + 1] = rgb.g;
    _Tensor[offs + 2] = rgb.b;
}
